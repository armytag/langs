Feature front, back, round
Feature height(low, lowmid, mid, highmid, high)
Feature type(*cons, vowel)
Feature voicing(unvoiced, voiced)
Feature place(labial, alveolar, palatal, velar, uvular, glottal)
Feature manner(nasal, stop, affricate, fricative, approximant, trill, tap)
Feature +long, +lateral, +aspirated, sibilant
Feature (syllable) +stress
Feature +hightone
Diacritic ́ [+hightone]
Diacritic ː [+long]
Diacritic ʰ [+aspirated]
Diacritic ' (before) [+stress]
Symbol i [high    +front -back -round vowel]
Symbol ɪ [highmid +front -back -round vowel]
Symbol ʏ [highmid +front -back +round vowel]
Symbol e [mid     +front -back -round vowel]
Symbol ɛ [lowmid  +front -back -round vowel]
Symbol œ [lowmid  +front -back +round vowel]
Symbol a [low     -front -back -round vowel]
Symbol u [high    -front +back +round vowel]
Symbol ʊ [highmid -front +back +round vowel]
Symbol o [mid     -front +back +round vowel]
Symbol ɔ [lowmid  -front +back +round vowel]
Symbol p [unvoiced labial stop]
Symbol t [unvoiced alveolar stop]
Symbol k [unvoiced velar stop]
Symbol q [unvoiced uvular stop]
Symbol ʔ [unvoiced glottal stop]
Symbol b [voiced labial stop]
Symbol d [voiced alveolar stop]
Symbol g [voiced velar stop]
Symbol m [labial nasal]
Symbol n [alveolar nasal]
Symbol ɲ [palatal nasal]
Symbol ŋ [velar nasal]
Symbol l [alveolar approximant +lateral]
Symbol r̥ [unvoiced alveolar trill]
Symbol r [voiced alveolar trill]
Symbol ɾ̥ [unvoiced alveolar tap]
Symbol ɾ [voiced alveolar tap]
Symbol f [unvoiced labial fricative -sibilant]
Symbol v [voiced labial fricative -sibilant]
Symbol ɬ [unvoiced alveolar fricative +lateral]
Symbol θ [unvoiced alveolar fricative -sibilant]
Symbol ð [voiced alveolar fricative -sibilant]
Symbol s [unvoiced alveolar fricative +sibilant]
Symbol ʃ [unvoiced palatal fricative +sibilant]
Symbol z [voiced alveolar fricative +sibilant]
Symbol x [unvoiced velar fricative -sibilant]
Symbol ɣ [voiced velar fricative -sibilant]
Symbol h [unvoiced glottal fricative -sibilant]
Symbol t͡ɕ [unvoiced palatal affricate]
Symbol d͡ʑ [voiced palatal affricate]
Symbol ʍ [unvoiced labial approximant]
Symbol w [voiced labial approximant]
Symbol j [voiced palatal approximant]

Syllables:
explicit

assign-stress:
# [vowel +long] => [+stress] / $ [cons]* [vowel] [cons]* _
# Else: <syl> => [+stress] / $ _
<syl> => [+stress] / $ _

nasal-stopping:
{[nasal] w, [nasal] r, [nasal] j} => {ʔ p, ʔ k, ʔ t} / [vowel] _

T93:
* => h / $ _ [vowel]
[vowel -long]$1 ʔ $1 => [+long] * *

T94:
[vowel]$1 [cons]$2 => $2 $1 / [vowel] _ {[cons], $}
Then: {(ɔ u), (e u), (e o), (i e)} => {(ɔː *), (j o), (j o), (j e)}
Then: j j => j *

T95:
[vowel]$1 [cons]$2 * [cons]$3 => $1 $2 $1 $3 / _ {[cons], $}

T97:
[cons !approximant]$1 [approximant]$2 => $2 $1 / [vowel]&[+stress] _
Then: [cons]$1 [cons]$2 [vowel]$3 => $1 $3 $2 / [vowel]&[+stress] _ $

T98:
[vowel +long] => [vowel +hightone -long]
Then: [vowel]&[+stress] => [+hightone] / _ [cons]* [vowel +hightone]

T99:
* => j / [vowel +stress] _ [cons] [vowel high +front]
* => w / [vowel +stress] _ [cons] [vowel high +back]

T100:
{([vowel +back high] w), ([vowel +front high] j)} => ([+hightone] *) / _ {[cons], $}
{[vowel mid], [vowel lowmid]} w => {[high -front +back +round], [mid -front +back +round]} * / _ {[cons], $}
{[vowel mid], [vowel lowmid]} j => {[high +front -back -round], [mid +front -back -round]} * / _ {[cons], $}

T101:
[vowel] => * / [vowel]&[+stress] [cons]? _ // _ [cons] {[cons], $}
Then: [vowel]$1 [cons]$2 => $2 $1 / [vowel]&[+stress] [cons] _ // _ $

Syllables:
[cons] {[approximant], [tap], [trill]}? [vowel] [cons]
[cons] {[approximant], [tap], [trill]}? [vowel] / _ {[cons] [vowel], $} // $ _ [cons] [vowel] [cons] [vowel]

remove-stress:
[+stress] => [-stress]

# last-step:
# unchanged
#
# Syllables:
# clear
