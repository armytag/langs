# For combining two files together
# pr -tmJS proto.wli proto-rand.wli > proto-compound.wli

Feature front, back
Feature height(low, mid, high)
Feature type(*cons, vowel)
Feature voicing(unvoiced, voiced)
Feature place(labial, alveolar, palatal, velar, glottal)
Feature manner(nasal, stop, fricative, approximant)
Feature +lateral
Feature +ejective
Feature tone(*neutral, hightone, lowtone)
Feature (syllable) +stress
Diacritic ʼ [+ejective]
Diacritic ́ [hightone]
Diacritic ̀ [lowtone]
Diacritic ' (before) [+stress]
Symbol a [low  -front +back vowel]
Symbol e [mid  +front -back vowel]
Symbol i [high +front -back vowel]
Symbol o [mid  -front +back vowel]
Symbol u [high -front +back vowel]
Symbol p [unvoiced labial stop]
Symbol t [unvoiced alveolar stop]
Symbol c [unvoiced palatal stop]
Symbol k [unvoiced velar stop]
Symbol ʔ [unvoiced glottal stop]
Symbol s [unvoiced alveolar fricative]
Symbol ʃ [unvoiced palatal fricative]
Symbol x [unvoiced velar fricative]
Symbol m [labial nasal]
Symbol n [alveolar nasal]
Symbol w [labial approximant]
Symbol j [palatal approximant]
Symbol l [alveolar approximant +lateral]

Syllables:
[cons] [vowel]

assign-stress:
<syl> => [+stress] / _ <syl> $

vowel-assimilation:
{i, u} => {e, o} / _ [cons] a

ejective-grassmann:
[+ejective] => [-ejective] / _ [vowel] {[+ejective], ʔ}

tonogenesis:
[vowel] => [hightone] / _ {[+ejective], ʔ}

romanizer-tonogenesis:
unchanged

tone-spread:
[neutral vowel] => [hightone] / _ [cons] [hightone vowel] [cons]&[+stress]

romanizer-tonespread:
unchanged

ejective-loss:
[+ejective] => [-ejective]

vowel-loss:
[vowel] => * / [vowel] [cons] _ $
[vowel] => * / [cons] [vowel] [cons] _ [cons]&[+stress]

Syllables:
[cons] [vowel]
[cons] [vowel] [cons]

romanizer-vowel-loss:
unchanged

glottal-stop-loss:
[vowel]$1 ʔ => $1 $1 // _ [vowel]
[cons]$1 ʔ => $1 $1
ʔ => h / _ [vowel]

Syllables:
[cons] [vowel]
[cons] [vowel] [cons]
[cons] [vowel] [vowel]

# move-stress:
# <syl> => [-stress]
# Then: <syl> => [+stress] / _ <syl> $

Syllables:
clear

romanizer:
unchanged
